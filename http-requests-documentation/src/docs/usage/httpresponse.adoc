=== Handling Responses

`HttpClient` request methods return `HttpResponse` objects that contain information about the response.

==== HttpResponse Properties

The set of response properties is smaller than that of the request, and provides basic information about the response

.HttpResponse Properties
[grid="rows", cols=[1,1,3]
|===
| Property         | Type          | Description

| `status`         | `Integer`     | HTTP status code of the response.
| `contentType`    | `String`      | Content-Type of the response, if the response contains an entity.
| `charSet`        | `String`      | Character set of the response, if the response contains an entity.
| `headers`        | `Map`         | Response headers.
| `allow`          | `List`        | Contains a list of `HttpMethod` objects. Typically returned from `HTTP OPTIONS`
                                     requests.
|===

==== Response Entities

The response entity can be retrieved by using the `getEntity()` method of the `HttpResponse`, which return an
`InputStream` containing the entity. By default, the response entity, if available, is buffered in the `HttpResponse`
and the response is closed. Subsequent calls to retrieve the response entity will reproduce the entity
an unlimited amount of times. If the request properties disabled this functionality, the response's entity can be read
only once, but it does not require the memory space to buffer the entity in the `HttpResponse` object. Additionally, it
is important to close the response when this functionality is disabled.

IMPORTANT: When `bufferResponseEntity` is `false`, it is very important that the `HttpResponse` is closed
when it is no longer needed. If it is not closed, underlying system resources may not be freed up. The `close()` method
can be called on the `HttpResponse` to properly clean up the request.

The `HttpResponse` supports <<Entity Converters,entity conversion>> using the `getEntity(Class<?> type)` method. If an
entity reader is registered that supports the requested type, it will be converted and returned.

==== HttpResponse Examples

===== Retrieving the Response as JSON

A very common use case is to make a request to an API service that responds with JSON. In this example, assume such an
API exists and returns a `Map` structure.

[source,groovy]
def client = httpClientFactory.createHttpClient()
def response = client.get { uri = "https://example.com/api/foo_resource" }
Map json = response.getEntity(Map)

===== Using the Response InputStream

Sometimes it may be beneficial to prevent the entity from being buffered or stored in memory at all, especially when
the entity is expected to be a large file. By disabling the buffering functionality and accessing the `InputStream`
directly, the server response can be passed directly through streams without buffering, depending on the type of
streams being used. The following example writes the response's `InputStream` directly to a file.

[source,groovy]
----
try {
    def response = httpClientFactory.createHttpClient().get {
        uri = "https://example.com/large_file.tar.gz"
        bufferResponseEntity = false
    }

    try {
        byte[] buffer = new byte[8192]
        int read
        InputStream in = response.getEntity()
        OutputStream out = new FileOutputStream("/path/to/file")

        try {
            while (true) {
                read = in.read(buffer)

                if (read == -1) {
                    break
                }

                out.write(buffer, 0, read)
            }
        }
        finally {
            in.close()
            out.close()
        }
    }
    finally {
        response.close()
    }
}
catch (HttpStatusException e) {
    e.response.close()
}
----

TIP: There are libraries that simplify shoveling data from one stream to another, such as IOUtils from Apache commons.

===== Retrieving the Response Entity as a String

The simplest of use cases of an HTTP request is to use the response's entity as a simple string. The following example
simply grabs the contents of Google's home page and prints it out to the system console.

[source,groovy]
def client = httpClientFactory.createHttpClient()
def response = client.get { uri = "http://google.com" }
println response.getEntity(String)

===== Handling Status Codes

Suppose an API service requires authentication, and the application must handle authentication failures. The following
example watches for `HTTP 401` response codes and takes an action based on that error case.

[source,groovy]
----
def retrieveData() {
    def client = httpClientFactory().createHttpClient()

    try {
        def response = client.get {
            uri = "https://example.com/api/example_resource"
            headers = ["X-Authentication": "some-authentication-token"]
        }
        return response.getEntity(Map)
    }
    catch (HttpUnauthorizedException e) {
        log.warn("call to retrieve data failed due to invalid authentication", e)
        return null
    }
}
----
